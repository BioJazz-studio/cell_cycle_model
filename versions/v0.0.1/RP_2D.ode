### MODEL NAME
# RP_2D

### MODEL NOTES
# This model can be used to reproduce Fig. 2B.
# The model shows how Cd flips the restriction point toggle between Rb and Ce (and tE2f).
# The variable terminology is described below and may sometimes differ from the project report, partially due to character limitations of XPP.


### Relation to other models:
# RP is the multidimensional sister model. RP_aa contains E2f autoactivation.


### Major assumptions:
# All reactions (even non elementary reactions) are best modelled with mass action kinetics for all reactions.
# Protein expression takes no time.
# Levels of tRb and tDna are constant. tRb degradation rate is incorporated in
# the corresponding dephosphorylation and dissociation rates.
# Cd and Ce have same phosphorylation rate constant. This constant is equal for E2fRb and free Rb.
# The unit [dna] is negligibly small compared to the unit [e2f].
# Transcription factors that are in complex with Dna are not accessible to degrading enzymes (and baseline degradation rates are included in the dissociation constant).


### Variables (quantities of dimension concentration given in the arbitrary units shown in square brackets):
# Rb, pRb: unphosphorylated (incl. E2fRb) and phosphorylated Retinoblastoma protein; [e2f]
# Ce: cyclin E-cdk2 complex; [e];
# E2f: free E2f (E2f that is not bound to Rb; includes E2f bound to DNA); [e2f]


### Parameter description:
# Cd: cyclin D-cdk4/6 complex; [e]
#   kDpRb, kPhRb: (de)phosphorylation rate of Rb; [1/min], [1/(e2f*min)]
# kSyCe1, kSyCe2: baseline and activated synthesis rates of Ce; [e/min]
#   K_E2fDna: dissociation constant of E2f from its target promoter; [e2f]
#   kDeCe: degradation rate of Ce; [1/min]
# tRb: total amount of Rb present in the system; [e2f]
# K_E2fRb: dissociation constant of the E2f:Rb protein complex; [e2f]
# tE2f: total E2f present in the system; [e2f]


### Idea behind parameter values:
# Parameters shall be biologically reasonable.
# Ce shall reach a theoretical maximum of 1.
# Lower bifurcation point shall be at Cd < 0, bistable range shall be large.


##################################################################################

### MODEL STATES
Rb' = kDpRb * pRb - kPhRb * (Cd + Ce) * Rb
Ce' = kSyCe1 + kSyCe2 * E2f / (K_E2fDna + E2f) - kDeCe*Ce


### INITIAL CONDITIONS (to be taken from mitotic exit)
init Rb = 1, Ce = 0


### ALGEBRAIC EQUATIONS
# Mass conservations:
pRb = tRb - Rb

# Quasi steady-state approximations:
E2f = (-B + sqrt(B^2+4*K_E2fRb*tE2f))/2

# Substitutions:
B=Rb-tE2f+K_E2fRb


### AUXILIARY QUANTITIES (for plotting purposes; indicated with underscore)
aux E2f_ = E2f
aux pRb_ = pRb


### MODEL PARAMETERS
par Cd = 0
  par kDpRb = 1, kPhRb = 3
par kSyCe1 = 0.01, kSyCe2 = 0.1
  par K_E2fDna = 0.1
  par kDeCe = 0.11
par tRb = 1
par K_E2fRb = 0.001
par tE2f = 0.5


### XppAut SETTINGS
@ Method=stiff, Total=1200, Bounds=100, Dt=1
@ Xp=Rb, Xlo=0, Xhi=1, Yp=Ce, Ylo=0, Yhi=1, Nmesh = 400

@ Ntst=150, Nmax=1000000, Npr=1000
@ Ds=.00001, Dsmin=.000001, Dsmax=.00005, ParMin=-1, ParMax=1
@ AutoVar=Ce, AutoXmin=-1, AutoXmax=1, AutoYmin=0, AutoYmax=1

done